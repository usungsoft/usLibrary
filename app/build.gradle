import proguard.gradle.ProGuardTask

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
group = 'com.github.usLibrary'

android {
    compileSdkVersion 32
    ndkVersion '22.1.7171670'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version '3.10.2'
        }
    }

    packagingOptions {
        pickFirst 'lib/x86_64/librfidLib.so'
        pickFirst 'lib/x86/librfidLib.so'
        pickFirst 'lib/arm64-v8a/librfidLib.so'
        pickFirst 'lib/armeabi-v7a/librfidLib.so'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task deleteJar(type: Delete) {
    delete 'build/release/usLibrary.jar'
}

task exportJar(type: Copy) {
    from('build/intermediates/aar_main_jar/release/')
    into('build/release/')
    include('classes.jar')
    rename('classes.jar', 'usLibrary.jar')
}

task deleteProguardJar(type: Delete) {
    delete 'build/release/compress'
}

task applyProguard(type: ProGuardTask) {
    configuration file('proguard-manual.pro')

    injars('build/release/usLibrary.jar')
    outjars('build/release/compress')
}

exportJar.dependsOn(deleteJar, build)

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.12'
    testImplementation project(path: ':app')
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'org.apache.commons:commons-lang3:3.6'

    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.annotation:annotation:1.0.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.usungsoft'
                artifactId = 'usLibrary'
                version = '1.1.7'
            }
        }
    }
}